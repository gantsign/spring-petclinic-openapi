plugins {
  id 'base'
  id 'com.diffplug.gradle.spotless'
  id 'org.openapi.generator'
  id "com.github.node-gradle.node"
}

task ensureTypeScriptDirectory {
  doLast {
    mkdir "$projectDir/generated"
  }
}

clean.doLast {
  file("$projectDir/generated").deleteDir()
}

openApiGenerate {
  generatorName = 'typescript-fetch'
  inputSpec = "$rootDir/server/src/main/resources/schema/petclinic.openapi.yaml".toString()
  outputDir = "$projectDir/generated".toString()
  configOptions = [
    npmName   : 'petclinic-api',
    npmVersion: '1.0.0'
  ]
}

spotless {
  groovyGradle {
    paddedCell() // Avoid cyclic ambiguities
    greclipse().configFile("$rootDir/config/eclipse-java-google-style.xml")
  }
}

node {
  download = true
  version = "12.20.0"
  nodeModulesDir = file("${project.projectDir}/generated")
}

npm_run_build {
  inputs.files fileTree('generated/src')

  inputs.file 'generated/package.json'
  inputs.file 'generated/package-lock.json'

  outputs.dir 'generated/build'
}

afterEvaluate {
  spotlessCheck.dependsOn spotlessApply
  tasks.openApiGenerate.dependsOn tasks.ensureTypeScriptDirectory
  npmInstall.dependsOn tasks.openApiGenerate
  assemble.dependsOn npm_run_build
}
