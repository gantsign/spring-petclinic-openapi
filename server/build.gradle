plugins {
  id 'checkstyle'
  id 'com.diffplug.gradle.spotless'
  id 'org.openapi.generator'
  id 'org.springframework.boot'
  id 'io.spring.dependency-management'
  id 'java'
  id 'jacoco'
  id 'nebula.lint'
}

if (!hasProperty('skipErrorProne')) {
  apply plugin: 'net.ltgt.errorprone'
}

allprojects {
  repositories {
    mavenCentral()
  }

  tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
  }

  test { jvmArgs '-XX:TieredStopAtLevel=1' }

  gradleLint {
    rules = [
      'all-nebula-renames',
      'duplicate-dependency-class',
      'unused-exclude-by-dep'
    ]
  }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}

configurations {
  checkstyleConfig
  developmentOnly
  runtimeClasspath {
    extendsFrom developmentOnly
  }
  compileOnly {
    extendsFrom annotationProcessor
  }
}

tasks.register('processClientResources', Copy) {
  def clientBuildDir = file("${project(':client').buildDir}")
  def clientResourcesDir = file("${project.buildDir}/resources/main/META-INF/web")

  group 'Client'
  description 'Process client resources'
  dependsOn ':client:assemble'

  from clientBuildDir
  into clientResourcesDir
}

tasks.named('processResources') {
  dependsOn tasks.named('processClientResources')
}

ext {
  set('checkstyleVersion', '8.30')
  set('mapstructVersion', '1.3.0.Final')
  set('lombokVersion', '1.18.12')
  set('jetbrainsAnnotationsVersion', '19.0.0')
}

dependencies {
  // Spring and Spring Boot dependencies
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-cache'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  compileOnly "org.projectlombok:lombok:${lombokVersion}"
  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
  gradleLint.ignore('duplicate-dependency-class') {
    annotationProcessor 'com.uber.nullaway:nullaway:0.7.9'
  }
  testImplementation "org.projectlombok:lombok:${lombokVersion}"
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
  testImplementation 'com.tngtech.archunit:archunit-junit4:0.13.1'

  implementation project(':server-generated')

  // Databases - Uses HSQL by default
  runtime 'org.hsqldb:hsqldb'
  // runtime 'mysql:mysql-connector-java'

  // EhCache
  implementation 'javax.cache:cache-api'
  implementation 'org.ehcache:ehcache'

  implementation "org.mapstruct:mapstruct:${mapstructVersion}"
  annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

  implementation 'com.atlassian.oai:swagger-request-validator-springmvc:2.5.0'

  compileOnly "org.jetbrains:annotations:${jetbrainsAnnotationsVersion}"
  testImplementation "org.jetbrains:annotations:${jetbrainsAnnotationsVersion}"
  compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

  checkstyleConfig("com.puppycrawl.tools:checkstyle:${checkstyleVersion}") { transitive = false }
  if (!hasProperty('skipErrorProne')) {
    errorprone 'com.google.errorprone:error_prone_core:2.4.0'
  }
}

compileJava {
  options.compilerArgs = [
    '-Amapstruct.suppressGeneratorTimestamp=true',
    '-Amapstruct.suppressGeneratorVersionInfoComment=true',
    '-Amapstruct.defaultComponentModel=spring'
  ]
}

tasks.withType(JavaCompile) {
  if (!hasProperty('skipErrorProne')) {
    options.errorprone.disableWarningsInGeneratedCode = true

    if (!name.toLowerCase().contains("test")) {
      options.errorprone {
        error("NullAway")
        option('NullAway:AnnotatedPackages', 'org.springframework.samples.petclinic')
        option('NullAway:TreatGeneratedAsUnannotated')
      }
    }
  }
}

bootJar {
  layered {
    application {
      intoLayer("spring-boot-loader") {
        include "org/springframework/boot/loader/**"
      }
      intoLayer("client") {
        include "META-INF/web/**"
      }
      intoLayer("application")
    }
    dependencies {
      intoLayer("application") {
        includeProjectDependencies()
      }
      intoLayer("snapshot-dependencies") {
        include "*:*:*SNAPSHOT"
      }
      intoLayer("dependencies")
      layerOrder = [
        "spring-boot-loader",
        "dependencies",
        "snapshot-dependencies",
        "application",
        "client",
      ]
    }
  }
}

spotless {
  java { googleJavaFormat('1.7') }
  groovyGradle {
    paddedCell() // Avoid cyclic ambiguities
    greclipse().configFile("${project.rootDir}/config/eclipse-java-google-style.xml")
  }
}

checkstyle {
  toolVersion = "${checkstyleVersion}"
  config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
  configProperties['org.checkstyle.google.suppressionfilter.config'] = file("${project.rootDir}/config/checkstyle-suppressions.xml").absolutePath
  ignoreFailures = false
  maxWarnings = 0
}

jacoco {
  toolVersion = '0.8.4'
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      element = 'SOURCEFILE'
      excludes = ['*Application.java']

      limit {
        counter = 'INSTRUCTION'
        value = 'COVEREDRATIO'
        minimum = 0.75
      }
    }
  }
}

jacocoTestReport {
  reports {
    xml.enabled true
    xml.destination file("${buildDir}/reports/jacoco/report.xml")
  }
}

afterEvaluate {
  if (!hasProperty('skipFormat')) {
    tasks.checkstyleMain.dependsOn tasks.spotlessApply
    tasks.checkstyleTest.dependsOn tasks.spotlessApply
    tasks.spotlessCheck.dependsOn tasks.spotlessApply
  } else {
    tasks.checkstyleMain.dependsOn tasks.spotlessCheck
    tasks.checkstyleTest.dependsOn tasks.spotlessCheck
  }
  if (System.env['CI'] == 'true') {
    tasks.jacocoTestReport.dependsOn tasks.jacocoTestCoverageVerification
    tasks.check.dependsOn tasks.jacocoTestReport
  }
  processResources.dependsOn ':client:assemble'
}
